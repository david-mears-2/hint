buildscript {
    ext.kotlin_version = '1.4.10'

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
    }
}

plugins {
    id 'org.springframework.boot' version '2.1.6.RELEASE' apply false
    id 'io.gitlab.arturbosch.detekt' version "1.14.0"
}

subprojects {

    repositories {
        mavenCentral()
        jcenter()
    }

    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.springframework.boot'
    apply plugin: "kotlin-spring"
    apply plugin: "io.gitlab.arturbosch.detekt"

    detekt {
        config = files("$rootDir/config/detekt/detekt.yml")
        buildUponDefaultConfig = true
        reports {
            html.enabled = true // observe findings in your browser with structure and code snippets
            xml.enabled = true // checkstyle like format mainly for integrations like Jenkins
            txt.enabled = true // similar to the console output, contains issue signature to manually edit baseline files
        }
    }

    tasks.detekt.jvmTarget = "1.8"

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    }

    jacoco {
        toolVersion = "0.8.3"
        reportsDir = file("$projectDir/coverage")
    }

    jacocoTestReport {
        reports {
            xml.enabled = true
            csv.enabled = false
        }
    }

    jar {
        enabled true
    }

    test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat = 'full'
        }
    }

    configurations {
        all*.exclude module : 'spring-boot-starter-logging'
    }

    group = 'org.imperial.mrc'
    sourceCompatibility = '1.8'
}
