buildscript {
    ext.kotlin_version = '1.7.10'

    repositories {
        mavenCentral()
        maven { // required for detekt < v1.17.0
            url "https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven"
        }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-allopen:$kotlin_version"
    }
}

plugins {
    id 'org.springframework.boot' version '2.7.2' apply false
    id 'io.gitlab.arturbosch.detekt' version "1.14.0"
}

subprojects {

    repositories {
        mavenCentral()
        maven { // required for detekt < v1.17.0
            url "https://maven.pkg.jetbrains.space/public/p/kotlinx-html/maven"
        }
    }

    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'org.springframework.boot'
    apply plugin: "kotlin-spring"
    apply plugin: "io.gitlab.arturbosch.detekt"

    detekt {
        config = files("$rootDir/config/detekt/detekt.yml")
        buildUponDefaultConfig = true
        reports {
            // these reports are generated in the build/reports/detekt directory
            txt.enabled = true
        }
    }

    tasks.detekt.jvmTarget = "1.8"

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    }

    jacoco {
        toolVersion = "0.8.8"
        reportsDirectory.set(layout.buildDirectory.dir("$projectDir/coverage"))
    }

    tasks.jacocoTestReport {
        reports {
            xml.required.set(true)
            csv.required.set(false)
        }
    }

    jar {
        enabled true
    }

    tasks.test {
        useJUnitPlatform()
        testLogging {
            exceptionFormat = 'full'
        }
        filter {
            excludeTestsMatching("org.imperial.mrc.hint.integration.adr.**")
        }
    }

    group = 'org.imperial.mrc'
    sourceCompatibility = '1.8'
}
